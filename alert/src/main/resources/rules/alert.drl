/*******************************************************************************
 * This file is part of OpenNMS(R).
 *
 * Copyright (C) 2019 The OpenNMS Group, Inc.
 * OpenNMS(R) is Copyright (C) 1999-2019 The OpenNMS Group, Inc.
 *
 * OpenNMS(R) is a registered trademark of The OpenNMS Group, Inc.
 *
 * OpenNMS(R) is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License,
 * or (at your option) any later version.
 *
 * OpenNMS(R) is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with OpenNMS(R).  If not, see:
 *      http://www.gnu.org/licenses/
 *
 * For more information contact:
 *     OpenNMS(R) Licensing <license@opennms.org>
 *     http://www.opennms.org/
 *     http://www.opennms.com/
 *******************************************************************************/

package org.opennms.horizon.alertservice.drools;

import java.util.Date;
import org.kie.api.time.SessionClock;
import org.opennms.horizon.alertservice.db.entity.Alert;
import org.opennms.horizon.alertservice.model.AlertSeverity;

global org.opennms.horizon.alertservice.api.AlertService alertService;

declare org.opennms.horizon.alertservice.db.entity.Alert
    @role(event)
    @timestamp(lastUpdateTime)
end

rule "cosmicClear"
  salience 100
  when
    $sessionClock : SessionClock()
    $clear : Alert(alertType == Alert.RESOLUTION_TYPE)
    $trigger : Alert(alertType == Alert.PROBLEM_TYPE, severity.isGreaterThanOrEqual(AlertSeverity.NORMAL), reductionKey == $clear.clearKey, lastEventTime <= $clear.lastEventTime)
  then
    alertService.clearAlert($trigger, new Date($sessionClock.getCurrentTime()));
end

rule "unclear"
  when
    $sessionClock : SessionClock()
    $trigger : Alert(alertType == Alert.PROBLEM_TYPE,
                         severity == AlertSeverity.CLEARED,
                         getLastEventSeverity().isGreaterThan(AlertSeverity.CLEARED),
                         lastEventTime > lastAutomationTime)
  then
    alertService.unclearAlert($trigger, new Date($sessionClock.getCurrentTime()));
end

/*
  An example of how the alert severity can be made to automatically increase over time.
  This rule is purposely commented out and disabled by default.

rule "escalation"
  when
    $sessionClock : SessionClock()
    $alert : Alert((alertType == Alert.PROBLEM_TYPE || alertType == Alert.PROBLEM_WITHOUT_RESOLUTION_TYPE) &&
                         AlertSeverity.WARNING.isLessThanOrEqual(severity) &&
                         AlertSeverity.CRITICAL.isGreaterThan(severity) &&
                         serviceType != null &&
                         alertAckTime == null)
    not( Alert( this == $alert ) over window:time( 1h ) )
  then
    alertService.escalateAlert($alert, new Date($sessionClock.getCurrentTime()));
end
*/

rule "cleanUp"
  salience 0
  when
    $sessionClock : SessionClock()
    $alert : Alert(severity.isLessThanOrEqual(AlertSeverity.NORMAL) &&
                       (alertAckTime == null))
    not( Alert( this == $alert ) over window:time( 5m ) )
  then
    alertService.deleteAlert($alert);
end

rule "fullCleanUp"
  salience 0
  when
    $sessionClock : SessionClock()
    $alert : Alert(severity.isLessThanOrEqual(AlertSeverity.NORMAL))
    not( Alert( this == $alert ) over window:time( 1d ) )
  then
    alertService.deleteAlert($alert);
end

rule "GC"
  salience 0
  when
    $sessionClock : SessionClock()
    $alert : Alert(alertAckTime == null)
    not( Alert( this == $alert ) over window:time( 3d ) )
  then
    alertService.deleteAlert($alert);
end

rule "fullGC"
  salience 0
  when
    $sessionClock : SessionClock()
    $alert : Alert()
    not( Alert( this == $alert ) over window:time( 8d ) )
  then
    alertService.deleteAlert($alert);
end
